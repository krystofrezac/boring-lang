BoringLang {
  space := " " | "\t" | newLine | oneLineComment | multiLineComment

  Program = | Program #(newLine) Program                                          -- statements
            | Statement                                                         -- endStatement

  Statement = | VariableDeclaration
              | Expression

  newLine = " "*"\n"" "*
  valueAssignmentOperator = "="
  typeAssignmentOperator = ":"
  oneLineComment = "//" (~newLine any)* 
  multiLineComment = "/*" (~"*/" any)* "*/"

  VariableDeclaration = | identifier ValueAssignment                            -- onlyValue
                        | identifier TypeAssignment ValueAssignment             -- valueAndType
                        | identifier TypeAssignment                             -- onlyType

  ValueAssignment = valueAssignmentOperator Expression
  TypeAssignment = typeAssignmentOperator TypeExpression 

  Expression = | FunctionCall
               | FunctionDeclaration<FunctionParameter, Expression>
               | numberExpression
               | stringExpression
               | booleanExpression 
               | identifier
               | Block

  TypeExpression = | FunctionDeclaration<TypeExpression, TypeExpression>
                   | stringType
                   | numberType
                   | booleanType
                   | identifier

  identifier = (letter | "!")+
  numberExpression = digit+
  stringExpression = "\"" ("\u{0021}" | "\u{0023}".."\u{00FF}")* "\""
  booleanExpression = trueExpression | falseExpression
  trueExpression = "true"
  falseExpression = "false"
  stringType = "string"
  numberType = "number"
  booleanType = "boolean"
  compilerHook  = "@"letter+

  Block = "{"  BlockStatement* "}"
  BlockStatement = | Statement #(newLine) BlockStatement                          -- statements
                   | Expression                                        -- endStatement

  FunctionDeclaration<TParameter, TReturnExpression> = "(" FunctionParameters<TParameter> ")" TReturnExpression
  FunctionParameters<TParameter> = ListOf<TParameter, ",">
  FunctionParameter = letter+


  FunctionCall = | FunctionCall"(" FunctionArguments ")"                        -- chainedCall
                 | identifier "(" FunctionArguments ")"                        -- firstCall
                 | compilerHook "(" FunctionArguments ")"                      -- firstCallCompilerHook
  FunctionArguments = ListOf<FunctionArgument, ",">
  FunctionArgument = Expression 

}